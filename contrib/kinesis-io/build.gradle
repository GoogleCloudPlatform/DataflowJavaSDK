group 'com.google.cloud.dataflow.sdk.io.kinesis'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

sourceSets {
    test {
        java {
            srcDir "src/test/unit/java"
        }
    }

    integration {
        java {
            srcDir 'src/test/integration/java'
        }
        compileClasspath += sourceSets.test.runtimeClasspath
    }

    e2e {
        java {
            srcDir 'src/test/e2e/java'
        }
        runtimeClasspath += sourceSets.integration.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
        compileClasspath += sourceSets.integration.runtimeClasspath
    }
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task integration(type: Test) {
    testClassesDir = sourceSets.integration.output.classesDir
    classpath += sourceSets.integration.runtimeClasspath
}

task e2e(type: Test) {
    testClassesDir = sourceSets.e2e.output.classesDir
    classpath += sourceSets.e2e.runtimeClasspath
}

dependencies {
    compile 'com.amazonaws:aws-java-sdk-kinesis:1.10.40'
    compile 'com.google.cloud.dataflow:google-cloud-dataflow-java-sdk-all:1.4.0'
    compile 'com.amazonaws:amazon-kinesis-client:1.6.1'

    testCompile 'com.google.apis:google-api-services-compute:v1-rev91-1.21.0'
    testCompile 'org.slf4j:slf4j-simple:1.7.13'
    testCompile 'org.easytesting:fest-assert:1.4'
    testCompile('com.amazonaws:amazon-kinesis-producer:0.10.2') {
        exclude module: 'aws-java-sdk-core'
    }
    testCompile 'com.amazonaws:aws-java-sdk-sts:1.10.40'
    testCompile('org.mockito:mockito-core:1.10.19') {
        exclude module: 'hamcrest-core'
    }
    testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
